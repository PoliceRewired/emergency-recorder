apply plugin: 'com.android.application'
apply plugin: 'com.google.android.gms.oss-licenses-plugin'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "org.policerewired.recorder"
        minSdkVersion 23
        targetSdkVersion 28
        versionCode 5
        versionName "0.5"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    flavorDimensions 'distribution'
    productFlavors {
        vanilla {
            dimension = 'distribution'
            applicationIdSuffix = '.vanilla'
        }
        appcenter {
            dimension = 'distribution'
            applicationIdSuffix = '.appcenter'
        }
    }

    /* jcodec also */
    configurations.all {
        resolutionStrategy.force 'com.google.code.findbugs:jsr305:3.0.2'
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "recorder-${variant.name}-${variant.versionName}.apk"
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.1.0-alpha04'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.google.android.material:material:1.1.0-alpha05'
    implementation "androidx.work:work-runtime:2.0.1"
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0-alpha04'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0-alpha04'

    /* ui components */
    implementation 'com.github.Kunzisoft:Android-SwitchDateTimePicker:2.0'

    /* common support libraries */
    implementation "commons-io:commons-io:2.6"
    implementation 'com.intellij:annotations:12.0'
    implementation 'com.google.android.gms:play-services-oss-licenses:16.0.2'

    /* background-service-lib */
    implementation 'com.flt.servicelib:background-service-lib:2.1'

    /* butterknife */
    implementation 'com.jakewharton:butterknife:10.1.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:10.1.0'

    /* camerakit */
    /* implementation 'com.camerakit:camerakit:1.0.0-beta3.11' */ // video support not included (yet)
    /* implementation 'com.wonderkiln:camerakit:0.13.4' */ // last version to support video
    /*
    implementation 'com.camerakit:jpegkit:0.1.0'
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.3.21'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.1.1'
     */

    /* camera view */
    implementation 'com.otaliastudios:cameraview:2.0.0-beta04'
    implementation 'androidx.exifinterface:exifinterface:1.0.0'

    /* jcodec - for creating a video from separate photos, see: http://jcodec.org/ */
    implementation 'org.jcodec:jcodec:0.2.3'
    implementation 'org.jcodec:jcodec-android:0.2.3'

    /* room db */
    // alpha04 is the last version that doesn't throw Kotlin errors.
    // alpha05, alpha06 tested and not working.
    implementation "androidx.room:room-runtime:2.1.0-alpha04"
    annotationProcessor "androidx.room:room-compiler:2.1.0-alpha04"
    testImplementation "androidx.room:room-testing:2.1.0-alpha04"

    /* location libraries */
    implementation "com.google.android.gms:play-services-location:16.0.0"
    implementation "com.what3words:w3w-java-wrapper:3.0.1"

    def appCenterSdkVersion = '1.11.4'
    implementation "com.microsoft.appcenter:appcenter-analytics:${appCenterSdkVersion}"
    implementation "com.microsoft.appcenter:appcenter-crashes:${appCenterSdkVersion}"
    implementation "com.microsoft.appcenter:appcenter-distribute:${appCenterSdkVersion}"
}
