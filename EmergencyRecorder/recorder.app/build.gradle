apply plugin: 'com.android.application'
apply plugin: 'com.google.android.gms.oss-licenses-plugin'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "org.policerewired.recorder"
        minSdkVersion 23
        targetSdkVersion 28
        versionCode 5
        versionName "0.5"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    flavorDimensions 'distribution'
    productFlavors {
        vanilla {
            dimension = 'distribution'
            applicationIdSuffix = '.vanilla'
        }
        appcenter {
            dimension = 'distribution'
            applicationIdSuffix = '.appcenter'
        }
    }

    /* jcodec also */
    configurations.all {
        resolutionStrategy.force 'com.google.code.findbugs:jsr305:3.0.2'
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "recorder-${variant.versionName}-${variant.name}.apk"
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.1.0-alpha05'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.google.android.material:material:1.1.0-alpha06'
    implementation "androidx.work:work-runtime:2.0.1"
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0-beta01'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0-beta01'

    /* ui components */
    implementation 'com.github.Kunzisoft:Android-SwitchDateTimePicker:2.0'
    implementation 'com.github.AppIntro:AppIntro:5.1.0'

    /* twitter */
    implementation 'com.twitter.sdk.android:tweet-ui:3.1.1'

    /* common support libraries */
    implementation "commons-io:commons-io:2.6"
    /* implementation 'com.intellij:annotations:12.0' */
    implementation 'com.google.android.gms:play-services-oss-licenses:16.0.2'

    /* background-service-lib */
    implementation 'com.flt.servicelib:background-service-lib:2.1'

    /* butterknife */
    implementation 'com.jakewharton:butterknife:10.1.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:10.1.0'

    /* camera view */
    implementation 'com.otaliastudios:cameraview:2.0.0-beta04'
    implementation 'androidx.exifinterface:exifinterface:1.0.0'

    /* jcodec - for creating a video from separate photos, see: http://jcodec.org/ */
    implementation 'org.jcodec:jcodec:0.2.3'
    implementation 'org.jcodec:jcodec-android:0.2.3'

    /* room db */
    // alpha04 is the last version that doesn't throw Kotlin errors.
    // alpha05, alpha06 tested and not working. alpha07 untested.
    // beta01 working!
    implementation "androidx.room:room-runtime:2.1.0-beta01"
    annotationProcessor "androidx.room:room-compiler:2.1.0-beta01"
    testImplementation "androidx.room:room-testing:2.1.0-beta01"

    /* location libraries */
    implementation "com.google.android.gms:play-services-location:16.0.0"
    implementation "com.what3words:w3w-java-wrapper:3.0.1"

    /* app center for testing */
    implementation "com.microsoft.appcenter:appcenter-analytics:1.11.4"
    implementation "com.microsoft.appcenter:appcenter-crashes:1.11.4"
    implementation "com.microsoft.appcenter:appcenter-distribute:1.11.4"
}
